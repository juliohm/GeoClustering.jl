# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    SLIC(k, m; tol=1e-4, maxiter=10)

A method for clustering geospatial data into approximately `k`
clusters using Simple Linear Iterative Clustering (SLIC).
The method produces clusters of samples that are spatially
connected based on a distance `dâ‚›` and that, at the same
time, are similar in terms of `vars` with distance `dáµ¥`.
The tradeoff is controlled with a hyperparameter parameter
`m` in an additive model `dâ‚œ = âˆš(dáµ¥Â² + mÂ²(dâ‚›/s)Â²)`.

## Parameters

* `k`       - Approximate number of clusters
* `m`       - Hyperparameter of SLIC model
* `tol`     - Tolerance of k-means algorithm (default to `1e-4`)
* `maxiter` - Maximum number of iterations (default to `10`)

## References

* Achanta et al. 2011. [SLIC superpixels compared to state-of-the-art
  superpixel methods](https://ieeexplore.ieee.org/document/6205760)
"""
struct SLIC <: ClusteringMethod
  k::Int
  m::Float64
  tol::Float64
  maxiter::Int
end

function SLIC(k::Int, m::Real; tol=1e-4, maxiter=10)
  @assert tol > 0 "invalid tolerance"
  @assert maxiter > 0 "invalid number of iterations"
  SLIC(k, m, tol, maxiter)
end

function partition(data, method::SLIC)
  # normalize atributes
  ð’¯ = TableDistances.normalize(values(data))
  Î© = georef(first(ð’¯), domain(data))

  # SLIC hyperparameter
  m = method.m

  # initial spacing of clusters
  s = slic_spacing(Î©, method)

  # initialize cluster centers
  c = slic_initialization(Î©, s)

  # ball neighborhood search
  searcher = BallSearch(Î©, NormBall(s))

  # pre-allocate memory for label and distance
  l = fill(0, nelements(Î©))
  d = fill(Inf, nelements(Î©))

  # performance parameters
  tol     = method.tol
  maxiter = method.maxiter

  # k-means algorithm
  err, iter = Inf, 0
  while err > tol && iter < maxiter
    o = copy(c)

    slic_assignment!(Î©, searcher, m, s, c, l, d)
    slic_update!(Î©, c, l)

    err = norm(c - o) / norm(o)
    iter += 1
  end

  subsets = [findall(isequal(k), l) for k in 1:length(c)]

  Partition(data, subsets)
end

function slic_spacing(data, method)
  V = measure(boundingbox(data))
  d = embeddim(data)
  k = method.k
  (V/k) ^ (1/d)
end

function slic_initialization(data, s)
  # efficient neighbor search
  searcher = KNearestSearch(data, 1)

  # bounding box properties
  bbox = boundingbox(data)
  lo, up = coordinates.(extrema(bbox))

  # cluster centers
  clusters = Vector{Int}()
  neighbor = Vector{Int}(undef, 1)
  ranges = [(l+s/2):s:u for (l, u) in zip(lo, up)]
  for x in Iterators.product(ranges...)
    search!(neighbor, Point(x), searcher)
    push!(clusters, neighbor[1])
  end

  unique(clusters)
end

function slic_assignment!(data, searcher, m, s, c, l, d)
  for (k, câ‚–) in enumerate(c)
    pâ‚– = centroid(data, câ‚–)
    inds = search(pâ‚–, searcher)

    # distance between points
    X  = (coordinates(centroid(data, ind)) for ind in inds)
    xâ‚– = [coordinates(pâ‚–)]
    dâ‚› = pairwise(Euclidean(), X, xâ‚–)

    # distance between variables
    ð’®áµ¢ = view(data, inds)
    ð’®â‚– = view(data, [câ‚–])
    V  = values(ð’®áµ¢)
    vâ‚– = values(ð’®â‚–)
    dáµ¥ = pairwise(TableDistance(normalize=false), V, vâ‚–)

    # total distance
    dâ‚œ = @. âˆš(dáµ¥^2 + m^2 * (dâ‚›/s)^2)

    @inbounds for (i, ind) in enumerate(inds)
      if dâ‚œ[i] < d[ind]
        d[ind] = dâ‚œ[i]
        l[ind] = k
      end
    end
  end
end

function slic_update!(data, c, l)
  for k in 1:length(c)
    inds = findall(isequal(k), l)
    X  = (coordinates(centroid(data, ind)) for ind in inds)
    Î¼  = [mean(X)]
    dâ‚› = pairwise(Euclidean(), X, Î¼)
    @inbounds c[k] = inds[argmin(vec(dâ‚›))]
  end
end
